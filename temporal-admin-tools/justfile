set export # Just variables are exported to environment variables
set fallback # Traverse upward in directory structure to find shared targets

[private]
default:
	just --list

[private]
start-local-registry:
	just --justfile=../justfile start-local-registry

[private]
push-to-local-registry version:
	just --justfile=../justfile push-to-local-registry "temporal-admin-tools" $version

# Clean the rockcraft project of the component and remove any existing rock files
clean version:
	cd "${version#temporal-admin-tools/}" && rockcraft clean
	cd "${version#temporal-admin-tools/}" && rm -f "$(ls | grep *.rock)"

# Pack a rock for the provided version
pack version debug="":
	just --justfile=../justfile pack temporal-admin-tools/${version} ${debug}

# Pack rock for component and run pod with rock in kubernetes
run version: (start-local-registry) (pack version) (push-to-local-registry version)
	#!/usr/bin/bash
	set -euxo pipefail

	trap 'just stop-local-registry' EXIT

	version="${version#temporal-admin-tools/}"

	extra_options="-e TEMPORAL_ROOT=/etc/temporal -e TEMPORAL_ENVIRONMENT=development-sqlite"
	goss_vars="GOSS_KUBECTL_BIN=\"$(which kubectl)\" GOSS_OPTS=\"--retry-timeout 60s --color\""
	
	eval "env ${goss_vars} kgoss edit -i localhost:5000/temporal-admin-tools-dev:${version} ${extra_options}"

# Pack rock for component and run goss tests with rock in kubernetes
test version: (start-local-registry) (pack version) (push-to-local-registry version)
	#!/usr/bin/bash
	set -euxo pipefail

	trap 'just stop-local-registry' EXIT

	version="${version#temporal-admin-tools/}"

	extra_options+="-e TEMPORAL_ROOT=/etc/temporal -e TEMPORAL_ENVIRONMENT=development-sqlite"
	goss_vars="GOSS_KUBECTL_BIN=\"$(which kubectl)\" GOSS_OPTS=\"--retry-timeout 60s\""

	eval "env ${goss_vars} kgoss run -i localhost:5000/temporal-admin-tools-dev:${version} ${extra_options}"
